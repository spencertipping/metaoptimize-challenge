#!/usr/bin/perl

Block merger.
This utility merges blocks by twos. Because it's the reduction step, all of the configuration goes here as well. Basically we take subsequent pairs of blocks from some directory (the blocks
can be of any size) and merge them into larger ones, discarding information in the process. While it is true that we will have arbitrarily many records this way, we can bound the size of each
one through a reduction step. This reduction step involves taking the ten most relevant words at any given point. Because we combine them only two at a time, this will ultimately be a fairly
accurate reflection of the actual similarity. (That is, we won't drop very many high-similarity words prematurely, though we will probably lose some.)

  Merging normalized records.
  This is surprisingly easy. Given two records:

  | w1 c11 p11 c12 p12 ...
    w1 c21 p21 c22 p22 ...

  We simply add the pij quantities for equal cij's, otherwise we leave them separate. Then we sort for greatest pij quantities and keep the first ten words. This new record will have the same
  format as the old one, but only the most relevant words will remain.

my %frequencies;
open my $vocabulary, '<', '../vocabulary.txt';
$frequencies{$2} = $1 for grep /(\d+) (.*)$/o, <$vocabulary>;
close $vocabulary;

Coefficients.
We need to configure sensible coefficients to determine how much each block contributes to the final result. For example, English text usually places adjectives before nouns; so it would make
sense for before- blocks to contribute more heavily than after- blocks. This isn't necessarily true for the before-connected and after-connected blocks; because we run one step in each
direction, these should be roughly equivalent.

my $connected_vs_adjacent = 0.5;
my $before_vs_after       = 0.8;

# Computed values:
my $absolute_connected_before = $connected_vs_adjacent * $before_vs_after;
my $absolute_connected_after  = $connected_vs_adjacent * (1 - $before_vs_after);
my $absolute_adjacent_before  = (1 - $connected_vs_adjacent) * $before_vs_after;
my $absolute_adjacent_after   = (1 - $connected_vs_adjacent) * (1 - $before_vs_after);

Blocks to be merged are provided as command-line arguments and are written to stdout.

my %records;
for (@ARGV) {
  print STDERR "Reading $_\n";
  my $coefficient = /^before-/ && /-connected$/ ? $absolute_connected_before :
                                  /-connected$/ ? $absolute_connected_after :
                    /^before-/                  ? $absolute_adjacent_before :
                                                  $absolute_adjacent_after;
  open my $file, '<', $_;
  for (<$file>) {
    my ($w, %links) = split;
    $records{$w} ||= {};
    $records{$w}{$_} += $links{$_} * $coefficient for keys %links;
  }
  close $file;
}

Now write the new normalized records in sorted order:

for my $w (sort keys %records) {
  my %record = %{$records{$w}};
  my @keys   = sort {$record{$b} <=> $record{$a}} keys %record;
  print "$w ", join(' ', grep defined, @keys[0 .. 9]), "\n";
}
